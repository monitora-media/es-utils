apply plugin: 'java-library'

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    // Update Elasticsearch version here
    classpath "org.elasticsearch.gradle:build-tools:${property("elasticsearch.version")}"
  }
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
    testImplementation(platform('org.junit:junit-bom:5.11.4'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

test {
    filter {
        include "**/*Test.class"
    }
    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    useJUnitPlatform {
        excludeTags 'performance'
    }
}

// Separate task for running performance tests
task performanceTest(type: Test) {
    description = 'Runs performance analysis tests for stemmers'
    group = 'verification'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    filter {
        include "**/*PerformanceTest.class"
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    useJUnitPlatform {
        includeTags 'performance'
    }
}

compileJava {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

apply plugin: 'elasticsearch.esplugin'

group = "cz.monitora.elasticsearch"
version = "${property("plugin.version")}-${property("elasticsearch.version")}"

esplugin {
  name = 'monitora_utils'
  description = 'Utils for Elasticsearch'
  classname = 'cz.monitora.elasticsearch.MonitoraESPlugin'
  licenseFile = rootProject.file('LICENSE')
}
